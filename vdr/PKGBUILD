# Maintainer: Senufo
pkgname=vdr
pkgver=2.0.3
pkgrel=3
pkgdesc="'open' digital satellite receiver and timer controlled video disk recorder"
url="http://tvdr.de/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libjpeg-turbo' 'perl' 'ttf-font')
optdepends=('lirc-utils: remote control support'
            'ncurses: skinncurses plugin'
            'runvdr-extreme: startscript for vdr')
provides=("vdr-api=2.0.0")
backup=('var/lib/vdr/channels.conf'
        'var/lib/vdr/diseqc.conf'
        'var/lib/vdr/keymacros.conf'
        'var/lib/vdr/scr.conf'
        'var/lib/vdr/sources.conf'
        'var/lib/vdr/svdrphosts.conf')
options='!emptydirs'
install='vdr.install'
source=("ftp://ftp.tvdr.de/vdr/${pkgname}-${pkgver}.tar.bz2"
        'vdr-loglevel-fix.diff::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=33745'
        'shutdown.sh'
        'shutdown-wrapper.c'
	'vdr-2.0.2-pin.diff')
md5sums=('dd8fb1642bdfa7feb0e9a8ae41e9ef2f'
         '6877c9efd84261c4a0471e657edce52d'
         'fb1f52d7decba2df9c5d00c70040984a'
         '7cad811b4ac5ee6c0b5496d006f1e0ee'
	 '7ba18be075f1e1c4e48d9186efbab963')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

cat > Make.config <<EOF
CFLAGS      += -O3
CXXFLAGS    += -O3
PREFIX       = /usr
LIBDIR       = /usr/lib/vdr/plugins
VDR_USER     = vdr
EOF

  patch -p1 -i "$srcdir/vdr-loglevel-fix.diff"
  patch -p0 -i "$srcdir/vdr-2.0.2-pin.diff"
}

build() {
  gcc -o shutdown-wrapper shutdown-wrapper.c

  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  install -Dm755 shutdown.sh "$pkgdir/usr/lib/vdr/bin/shutdown.sh"

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
