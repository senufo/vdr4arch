# Maintainer: Senufo
pkgname=vdr
pkgver=2.0.5
pkgrel=1
pkgdesc="'open' digital satellite receiver and timer controlled video disk recorder, pin patch"
url="http://tvdr.de/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libjpeg-turbo' 'perl' 'ttf-font')
optdepends=('lirc-utils: remote control support'
            'ncurses: skinncurses plugin'
            'runvdr-extreme: startscript for vdr')
provides=("vdr-api=2.0.0")
backup=('var/lib/vdr/channels.conf'
        'var/lib/vdr/diseqc.conf'
        'var/lib/vdr/keymacros.conf'
        'var/lib/vdr/scr.conf'
        'var/lib/vdr/sources.conf'
        'var/lib/vdr/svdrphosts.conf')
options='!emptydirs'
install='vdr.install'
source=("ftp://ftp.tvdr.de/vdr/${pkgname}-${pkgver}.tar.bz2"
        'shutdown.sh'
        'shutdown-wrapper.c'
	'vdr-2.0.2-pin.diff'
        'jumpseconds.patch')

md5sums=('f58b9836a60440fde84480a2e37ee91d'
         'fb1f52d7decba2df9c5d00c70040984a'
         '7cad811b4ac5ee6c0b5496d006f1e0ee'
	 '7ba18be075f1e1c4e48d9186efbab963'
         '386ca0cd63e6620f241d9c7de397df79')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

cat > Make.config <<EOF
PREFIX    = /usr
BINDIR    = /usr/bin
INCDIR    = /usr/include
LIBDIR    = /usr/lib/vdr/plugins
LOCDIR    = /usr/share/locale
MANDIR    = /usr/share/man
PCDIR     = /usr/lib/pkgconfig
RESDIR    = /usr/share/vdr
VIDEODIR  = /srv/vdr/video
CONFDIR   = /var/lib/vdr
CACHEDIR  = /var/cache/vdr

VDR_USER     = henri
### Fallback for plugins with old makefiles:
ifdef PLUGIN
CFLAGS   += -fPIC
CXXFLAGS += -fPIC
endif
EOF

  patch -p0 -i "$srcdir/vdr-2.0.2-pin.diff"
  patch -p1 -i "$srcdir/jumpseconds.patch"

}

build() {
  gcc -o shutdown-wrapper shutdown-wrapper.c

  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  install -Dm755 shutdown.sh "$pkgdir/usr/lib/vdr/bin/shutdown.sh"

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
